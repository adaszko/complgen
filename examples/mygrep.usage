mygrep [<OPTION>]... [--] <PATTERNS> [<PATH>]...;

<OPTION> ::= (--extended-regexp || -E)                                                      "PATTERNS are extended regular expressions"
           | (--fixed-strings || -F)                                                        "PATTERNS are strings"
           | (--basic-regexp || -G)                                                         "PATTERNS are basic regular expressions"
           | (--perl-regexp || -P)                                                          "PATTERNS are Perl regular expressions"
           | (--regexp=<PATTERNS> || -e <PATTERNS>)                                         "use PATTERNS for matching"
           | (--file=<PATH> || -f <PATH>)                                                   "take PATTERNS from FILE"
           | (--ignore-case || -i)                                                          "ignore case distinctions in patterns and data"
           | --no-ignore-case                                                               "do not ignore case distinctions (default)"
           | (--word-regexp || -w)                                                          "match only whole words"
           | (--line-regexp || -x)                                                          "match only whole lines"
           | (--null-data || -z)                                                            "a data line ends in 0 byte, not newline"
           | (--no-messages || -s)                                                          "suppress error messages"
           | (--invert-match || -v)                                                         "select non-matching lines"
           | (--version || -V)                                                              "display version information and exit"
           | --help                                                                         "display this help text and exit"
           | (--max-count=<NUM> || --max-count <NUM> | -m <NUM>)                            "stop after NUM selected lines"
           | (--byte-offset || -b)                                                          "print the byte offset with output lines"
           | (--line-number || -n)                                                          "print line number with output lines"
           | --line-buffered                                                                "flush output on every line"
           | (--with-filename || -H)                                                        "print file name with output lines"
           | (--no-filename || -h)                                                          "suppress the file name prefix on output"
           | (--label=<LABEL>)                                                              "use LABEL as the standard input file name prefix"
           | (--only-matching || -o)                                                        "show only nonempty parts of lines that match"
           | (--quiet | --silent || -q)                                                     "suppress all normal output"
           | (--binary-files=<TYPE>)                                                        "assume that binary files are <TYPE>"
           | (--text || -a)                                                                 "equivalent to --binary-files=text"
           | (--directories=<ACTION-DIRECTORIES> || -d <ACTION-DIRECTORIES>)                "how to handle directories"
           | (--devices=<ACTION-DEVICES> || -D <ACTION-DEVICES>)                            "how to handle devices, FIFOs and sockets"
           | (--recursive || -r)                                                            "like --directories=recurse"
           | (--dereference-recursive || -R)                                                "like --directories=recurse, but follow all symlinks"
           | (--include=<GLOB>)                                                             "search only files that match GLOB (a file pattern)"
           | (--exclude=<GLOB>)                                                             "skip files that match GLOB"
           | (--exclude-from=<PATH>)                                                        "skip files that match any file pattern from FILE"
           | (--exclude-dir=<GLOB>)                                                         "skip directories that match GLOB"
           | (--files-without-match || -L)                                                  "print only names of FILEs with no selected lines"
           | (--files-with-matches || -l)                                                   "print only names of FILEs with selected lines"
           | (--count || -c)                                                                "print only a count of selected lines per FILE"
           | (--initial-tab || -T)                                                          "make tabs line up (if needed)"
           | (--null || -Z)                                                                 "print 0 byte after FILE name"
           | (--before-context=<NUM> || -B <NUM>)                                           "print NUM lines of leading context"
           | (--after-context=<NUM> || -A <NUM>)                                            "print NUM lines of trailing context"
           | (--context=<NUM> || -C <NUM> | -<NUM>)                                         "print NUM lines of output context"
           | --group-separator=<SEP>                                                        "print SEP on line between matches with context"
           | --no-group-separator                                                           "do not print separator for matches with context"
           | (--color=[<WHEN>] || --color [<WHEN>] | --colour=[<WHEN>] | --colour [<WHEN>]) "use markers to highlight the matching strings"
           | (--binary || -U)                                                               "do not strip CR characters at EOL (MSDOS/Windows)"
           ;

<ACTION-DIRECTORIES> ::= read       "read directories in the same manner as normal files"
                       | recurse    "read directories recursively (same effect as -R and -r option)"
                       | skip       "silently ignore the directories";

<ACTION-DEVICES> ::= read "read devices as if they were normal files"
                   | skip "devices are skipped silently";

<TYPE> ::= binary           "Search binary files but do not print them"
         | text             "Treat all files as text"
         | without-match    "Do not search binary files";

<WHEN> ::= always | never | auto "only when stdout is a tty";
